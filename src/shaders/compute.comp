#version 450
#extension GL_ARB_separate_shader_objects : enable

#define PI_OVER_FOUR 0.785398163f

#define WORKGROUP_SIZE 32
layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform CameraBufferObject {
    mat4 view;
    mat4 proj;
} camera;

layout(set = 1, binding = 0) uniform Time {
    float deltaTime;
    float totalTime;
};

struct Blade {
    vec4 v0;
    vec4 v1;
    vec4 v2;
    vec4 up;
};

layout(set = 2, binding = 0) buffer Blades {
	  Blade blades[];
};

layout(set = 2, binding = 1) buffer CulledBlades {
	  Blade culledBlades[];
};

layout(set = 2, binding = 2) buffer NumBlades {
    uint vertexCount;   // accumulate num blades remaining
    uint instanceCount; // = 1
    uint firstVertex;   // = 0
    uint firstInstance; // = 0
} numBlades;

bool inBounds(float value, float bounds) {
    return (value >= -bounds) && (value <= bounds);
}

vec4 random4(vec4 p) {
    return fract(sin(vec4(dot(p, vec4(127.1, 311.7, 921.5, 465.8)),
                          dot(p, vec4(269.5, 183.3, 752.4, 429.1)),
                          dot(p, vec4(420.6, 631.2, 294.3, 910.8)),
                          dot(p, vec4(213.7, 808.1, 126.8, 572.0))
                    )) * 43758.5453);
}

float surflet(vec4 p, vec4 gridPoint) {
    vec4 t2 = abs(p - gridPoint);
    vec4 t = vec4(1.f) - 6.f * pow(t2, vec4(5.f)) + 15.f * pow(t2, vec4(4.f)) - 10.f * pow(t2, vec4(3.f));
    vec4 gradient = random4(gridPoint) * 2 - vec4(1);
    vec4 diff = p - gridPoint;
    float height = dot(diff, gradient);
    return height * t.x * t.y * t.z * t.w;
}

float perlin(vec4 p) {
    float surfletSum = 0.f;
    for (int dx = 0; dx <= 1; ++dx) {
        for (int dy = 0; dy <= 1; ++dy) {
            for (int dz = 0; dz <= 1; ++dz) {
                for (int dw = 0; dw <= 1; ++dw) {
                    surfletSum += surflet(p, floor(p) + vec4(dx, dy, dz, dw));
                }
            }
        }
    }
    return surfletSum;
}

const vec3 gE = vec3(0, -3, 0);

const vec3 windNoiseMoveDirection = normalize(vec3(2, 0.2, 1)) * 0.7;

void main() {
    const uint bladeIdx = gl_GlobalInvocationID.x;
    
    if (bladeIdx == 0) {
        numBlades.vertexCount = 0;
    }
    barrier();

    // -------------------------
    // APPLY FORCES
    // -------------------------
    Blade thisBlade = blades[bladeIdx];

    vec3 v0 = thisBlade.v0.xyz;
    vec3 v1 = thisBlade.v1.xyz;
    vec3 v2 = thisBlade.v2.xyz;
    const vec3 up = thisBlade.up.xyz;

    const float theta = thisBlade.v0.w;
    const float h = thisBlade.v1.w;
    const float stiffness = thisBlade.up.w;

    // recovery
    vec3 Iv2 = v0 + h * up;
    vec3 Fr = (Iv2 - v2) * stiffness;

    // gravity
    vec3 f = vec3(cos(theta + PI_OVER_FOUR), 0, sin(theta + PI_OVER_FOUR));
    vec3 gF = 0.25 * length(gE) * f;
    vec3 Fg = gE + gF;

    // wind
    vec3 Fw = vec3(
        perlin(vec4(v2 * 0.4 + windNoiseMoveDirection * totalTime, totalTime * 0.3)), 
        0, 
        perlin(vec4((v2 * 0.4 + vec3(493.12, 210.03, 974.54)) + windNoiseMoveDirection * totalTime, totalTime * 0.3))
    ) * 10;

    vec3 delta = (Fr + Fg + Fw) * deltaTime;

    v2 += delta;
    v2 = v2 - up * min(up * (v2 - v0), 0);
    float lProj = length(v2 - v0 - up * dot(v2 - v0, up));
    v1 = v0 + h * up * max(1 - lProj / h, 0.05 * max(lProj / h, 1));

    float L0 = distance(v0, v2);
    float L1 = distance(v0, v1) + distance(v1, v2);
    // int n = 3;
    // float L = (2 * L0 + (n - 1) * L1) / (n + 1);
    float L = (L0 + L1) * 0.5;

    float r = h / L;
    vec3 v1_corr = v0 + r * (v1 - v0);
    vec3 v2_corr = v1_corr + r * (v2 - v1);

    thisBlade.v1.xyz = v1_corr.xyz;
    thisBlade.v2.xyz = v2_corr.xyz;
    blades[bladeIdx] = thisBlade;

    // -------------------------
    // CULLING
    // -------------------------

    // TODO: early exit here if blade is culled

    const uint culledIdx = atomicAdd(numBlades.vertexCount, 1);
    culledBlades[culledIdx] = thisBlade;
}
